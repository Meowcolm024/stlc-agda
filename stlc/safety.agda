module stlc.safety where

open import stlc.base
open import stlc.prop
open import stlc.subst

import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl)
open import Data.Nat using (â„•; zero; suc)
open import Data.Fin using (Fin) renaming (zero to fz; suc to fs)
open import Data.Product using (_Ã—_; _,_; âˆƒ-syntax; Î£-syntax)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚) renaming ([_,_] to case-âŠ)
open import Relation.Nullary using (Â¬_; contradiction; Dec; yes; no)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.Unit using (âŠ¤; tt)

private
  variable
    n m : â„•

open multistep

Safe : Term 0 â†’ Set
Safe M = âˆ€ M' â†’ M â€”â†’* M' â†’ Value M' âŠ âˆƒ[ N ] (M' â€”â†’ N)

infix  6 â€”â†›_

-- irreducible term
â€”â†›_ : Term 0 â†’ Set
â€”â†›_ M = âˆ€ M' â†’ Â¬ (M â€”â†’ M')

-- logical predicate for semantic value/term
ğ’±âŸ¦_âŸ§ : Type â†’ Term 0 â†’ Set
â„°âŸ¦_âŸ§ : Type â†’ Term 0 â†’ Set

ğ’±âŸ¦ bool  âŸ§ true  = âŠ¤
ğ’±âŸ¦ bool  âŸ§ false = âŠ¤
ğ’±âŸ¦ A â‡’ B âŸ§ (Æ› M) = âˆ€ N â†’ ğ’±âŸ¦ A âŸ§ N â†’ â„°âŸ¦ B âŸ§ (M [ N ])
ğ’±âŸ¦ _     âŸ§ _     = âŠ¥

â„°âŸ¦ A âŸ§ M = âˆ€ M' â†’ (M â€”â†’* M') Ã— (â€”â†› M') â†’ ğ’±âŸ¦ A âŸ§ M'

-- well typed substitution
ğ’¢âŸ¦_âŸ§ : Context n â†’ (Fin n â†’ Term 0) â†’ Set
ğ’¢âŸ¦ Î“ âŸ§ Ïƒ = âˆ€ {x A} â†’ Î“ âˆ‹ x â¦‚ A â†’ ğ’±âŸ¦ A âŸ§ (Ïƒ x)

infix  4 _âŠ¨_â¦‚_

-- semantic typing
_âŠ¨_â¦‚_ : Context n â†’ Term n â†’ Type â†’ Set
Î“ âŠ¨ M â¦‚ A = âˆ€ Ïƒ â†’ ğ’¢âŸ¦ Î“ âŸ§ Ïƒ â†’ â„°âŸ¦ A âŸ§ (subst Ïƒ M)

value? : (M : Term 0) â†’ Dec (Value M)
value? (Æ› M)      = yes V-abs
value? (M Â· N)    = no Î» ()
value? true       = yes V-true
value? false      = yes V-false
value? (if L M N) = no Î» ()

reducible? : (M : Term 0) â†’ Dec (âˆƒ[ M' ] M â€”â†’ M')
reducible? (Æ› M) = no Î» ()
reducible? (M Â· N) with reducible? M
... | yes (M' , Mâ†’M') = yes (M' Â· N , Î¾-appâ‚ Mâ†’M')
reducible? ((Æ› M) Â· N)  | no â€”â†›M with reducible? N
... | yes (N' , Nâ†’N') = yes ((Æ› M) Â· N' , Î¾-appâ‚‚ Nâ†’N')
... | no  â€”â†›N with value? N
...   | yes VN  = yes ((M [ N ]) , Î²-abs VN)
...   | no  Â¬VN = no Î» { (_ , Î¾-appâ‚‚ Nâ†’N') â†’ â€”â†›N (_ , Nâ†’N') ; (_ , Î²-abs VN) â†’ Â¬VN VN }
reducible? (Mâ‚ Â· Mâ‚‚ Â· N)     | no â€”â†›M = no Î» { (_ , Î¾-appâ‚ Mâ†’M') â†’ â€”â†›M (_ , Mâ†’M') }
reducible? (true Â· N)        | no â€”â†›M = no Î» { (_ , Î¾-appâ‚ Mâ†’M') â†’ â€”â†›M (_ , Mâ†’M') }
reducible? (false Â· N)       | no â€”â†›M = no Î» { (_ , Î¾-appâ‚ Mâ†’M') â†’ â€”â†›M (_ , Mâ†’M') }
reducible? (if Mâ‚ Mâ‚‚ Mâ‚ƒ Â· N) | no â€”â†›M = no Î» { (_ , Î¾-appâ‚ Mâ†’M') â†’ â€”â†›M (_ , Mâ†’M') }
reducible? true       = no Î» ()
reducible? false      = no Î» ()
reducible? (if L M N) with reducible? L
... | yes (M' , Mâ†’M') = yes (if M' M N , Î¾-if Mâ†’M')
... | no  â€”â†›M with L
...   | Æ› Lâ‚          = no Î» { (_ , Î¾-if ifâ†’if') â†’ â€”â†›M (_ , ifâ†’if') }
...   | Lâ‚ Â· Lâ‚‚       = no Î» { (_ , Î¾-if ifâ†’if') â†’ â€”â†›M (_ , ifâ†’if') }
...   | true          = yes (M , Î²-ifâ‚)
...   | false         = yes (N , Î²-ifâ‚‚)
...   | if Lâ‚ Lâ‚‚ Lâ‚ƒ   = no Î» { (_ , Î¾-if ifâ†’if') â†’ â€”â†›M (_ , ifâ†’if') }

ğ’±âŸ¦boolâŸ§-Canonical : âˆ€ {M} â†’ ğ’±âŸ¦ bool âŸ§ M â†’ M â‰¡ true âŠ M â‰¡ false
ğ’±âŸ¦boolâŸ§-Canonical {M = true}  VM = injâ‚ refl
ğ’±âŸ¦boolâŸ§-Canonical {M = false} VM = injâ‚‚ refl

ğ’±âŸ¦â‡’âŸ§-Canonical : âˆ€ {M A B} â†’ ğ’±âŸ¦ A â‡’ B âŸ§ M â†’ âˆƒ[ N ] (M â‰¡ Æ› N)
ğ’±âŸ¦â‡’âŸ§-Canonical {M = Æ› M} VM = M , refl

â€”â†›-Mâ†’*M : âˆ€ {M M'} â†’ M â€”â†’* M' â†’ â€”â†› M â†’ M â‰¡ M'
â€”â†›-Mâ†’*M (_ âˆ)                â€”â†›M = refl
â€”â†›-Mâ†’*M (_ â€”â†’âŸ¨ Mâ†’Mâ‚ âŸ© Mâ†’*M') â€”â†›M = âŠ¥-elim (â€”â†›M _ Mâ†’Mâ‚)

Vâ†’â€”â†› : âˆ€ {M : Term 0} â†’ Value M â†’ â€”â†› M
Vâ†’â€”â†› V-abs   M' ()
Vâ†’â€”â†› V-true  M' ()
Vâ†’â€”â†› V-false M' ()

ğ’±-V : âˆ€ {M A} â†’ ğ’±âŸ¦ A âŸ§ M â†’ Value M
ğ’±-V {M = true}  {A = bool}  VM = V-true
ğ’±-V {M = false} {A = bool}  VM = V-false
ğ’±-V {M = Æ› M}   {A = A â‡’ B} VM = V-abs

ğ’±â†’â€”â†› : âˆ€ {M A} â†’ ğ’±âŸ¦ A âŸ§ M â†’ â€”â†› M
ğ’±â†’â€”â†› VM = Vâ†’â€”â†› (ğ’±-V VM)

-- irreducible application implies its lhs is irreducible
appLâ€”â†› : âˆ€ {M N V} â†’ M Â· N â€”â†’* V â†’ â€”â†› V â†’ âˆƒ[ M' ] (M â€”â†’* M') Ã— (â€”â†› M')
appLâ€”â†› ((M Â· N) âˆ)                         â€”â†›V = M , (M âˆ) , Î» { M' x â†’ â€”â†›V (M' Â· N) (Î¾-appâ‚ x) }
appLâ€”â†› ((M Â· N) â€”â†’âŸ¨ Î¾-appâ‚ Mâ†’M' âŸ© MÂ·Nâ†’*V)  â€”â†›V with M' , Mâ†’*M' , â€”â†›M' â† appLâ€”â†› MÂ·Nâ†’*V â€”â†›V = M' , stepâ€”â†’ M Mâ†’*M' Mâ†’M' , â€”â†›M'
appLâ€”â†› ((M Â· N) â€”â†’âŸ¨ Î¾-appâ‚‚ Nâ†’N' âŸ© MÂ·Nâ†’*V)  â€”â†›V = appLâ€”â†› MÂ·Nâ†’*V â€”â†›V
appLâ€”â†› (((Æ› M) Â· N) â€”â†’âŸ¨ Î²-abs VN âŸ© MÂ·Nâ†’*V) â€”â†›V = Æ› M , (Æ› M âˆ) , Î» { M' () }

appRâ€”â†› : âˆ€ {M N V} â†’ (Æ› M) Â· N â€”â†’* V â†’ â€”â†› V â†’ âˆƒ[ N' ] (N â€”â†’* N') Ã— (â€”â†› N')
appRâ€”â†› (((Æ› M) Â· N) âˆ)                        â€”â†›V = N , (N âˆ) , Î» { M' x â†’ â€”â†›V ((Æ› M) Â· M') (Î¾-appâ‚‚ x) }
appRâ€”â†› (((Æ› M) Â· N) â€”â†’âŸ¨ Î¾-appâ‚‚ Nâ†’N' âŸ© MÂ·Nâ†’*V) â€”â†›V with N' , Nâ†’*N' , â€”â†›N' â† appRâ€”â†› MÂ·Nâ†’*V â€”â†›V = N' , stepâ€”â†’ N Nâ†’*N' Nâ†’N' , â€”â†›N'
appRâ€”â†› (((Æ› M) Â· N) â€”â†’âŸ¨ Î²-abs VN âŸ© MÂ·Nâ†’*V)    â€”â†›V = N , (N âˆ) , Vâ†’â€”â†› VN

ifâ€”â†› : âˆ€ {L M N V} â†’ if L M N â€”â†’* V â†’ â€”â†› V â†’ âˆƒ[ L' ] (L â€”â†’* L') Ã— (â€”â†› L')
ifâ€”â†› ((if L M N) âˆ)                    â€”â†›V = L , (L âˆ) , (Î» M' x â†’ â€”â†›V (if M' M N) (Î¾-if x))
ifâ€”â†› ((if L M N) â€”â†’âŸ¨ Î¾-if VL âŸ© ifâ†’*V)  â€”â†›V with L' , Lâ†’L' , â€”â†›L' â† ifâ€”â†› ifâ†’*V â€”â†›V = L' , (L â€”â†’âŸ¨ VL âŸ© Lâ†’L') , â€”â†›L'
ifâ€”â†› ((if true M N) â€”â†’âŸ¨ Î²-ifâ‚ âŸ© Mâ†’*V)  â€”â†›V = true , (true âˆ) , Î» { x () }
ifâ€”â†› ((if false M N) â€”â†’âŸ¨ Î²-ifâ‚‚ âŸ© Nâ†’*V) â€”â†›V = false , (false âˆ) , Î» {x () }

confâ€”â†›join : âˆ€ {L M M'} â†’ L â€”â†’* M â†’ L â€”â†’* M' â†’ â€”â†› M â†’ M' â€”â†’* M
confâ€”â†›join Lâ†’*M Lâ†’*M' â€”â†›M
  with N , Mâ†’*N , M'â†’*N â† confluence Lâ†’*M Lâ†’*M'
  with refl â† â€”â†›-Mâ†’*M Mâ†’*N â€”â†›M
  = M'â†’*N

-- fundamental property
-- syntactic typing implies semantic typing
âŠ¢-âŠ¨ : âˆ€ {Î“ : Context n} {M A}
  â†’ Î“ âŠ¢ M â¦‚ A
    ----------
  â†’ Î“ âŠ¨ M â¦‚ A

âŠ¢-âŠ¨ (âŠ¢var x) Ïƒ GG M' (Mâ†’*M' , â€”â†›M') with refl â† â€”â†›-Mâ†’*M Mâ†’*M' (ğ’±â†’â€”â†› (GG x)) = GG x
âŠ¢-âŠ¨ {M = Æ› M} (âŠ¢abs âŠ¢M) Ïƒ GG M' ((Æ› âŸªÏƒâŸ«M âˆ) , â€”â†›M') N VN M'' (MNâ†’M' , â€”â†›M'')
  = âŠ¢-âŠ¨ âŠ¢M (N â€¢ Ïƒ) (Î» { Z â†’ VN ; (S x) â†’ GG x }) M'' (âŸªNâ€¢ÏƒâŸ«Mâ†’*M'' , â€”â†›M'')
  where
    âŸªNâ€¢ÏƒâŸ«Mâ†’*M'' : subst (N â€¢ Ïƒ) M â€”â†’* M''
    âŸªNâ€¢ÏƒâŸ«Mâ†’*M'' rewrite Eq.sym (sub-ext-sub {Ïƒ = Ïƒ} {M = M} {N = N}) = MNâ†’M'
âŠ¢-âŠ¨ {M = M Â· N} (âŠ¢app âŠ¢M âŠ¢N) Ïƒ GG M' (Mâ†’*M' , â€”â†›M')
  with Mâ‚ , ÏƒMâ†’Mâ‚ , â€”â†›Mâ‚ â† appLâ€”â†› Mâ†’*M' â€”â†›M'
  with VM â† âŠ¢-âŠ¨ âŠ¢M Ïƒ GG Mâ‚ (ÏƒMâ†’Mâ‚ , â€”â†›Mâ‚)
  with Mâ‚' , refl â† ğ’±âŸ¦â‡’âŸ§-Canonical VM
  with Mâ‚Â·ÏƒNâ†’*M' â† confâ€”â†›join Mâ†’*M' (â€”â†’*-trans (appL-cong ÏƒMâ†’Mâ‚) (Mâ‚ Â· subst Ïƒ N âˆ)) â€”â†›M'
  with Nâ‚ , ÏƒNâ†’Nâ‚ , â€”â†›Nâ‚ â† appRâ€”â†› Mâ‚Â·ÏƒNâ†’*M' â€”â†›M'
  with VN â† âŠ¢-âŠ¨ âŠ¢N Ïƒ GG Nâ‚ (ÏƒNâ†’Nâ‚ , â€”â†›Nâ‚)
  = VM Nâ‚ VN M' (Mâ‚[Nâ‚]â†’*Mâ‚‚ , â€”â†›M')
  where
    Mâ‚[Nâ‚]â†’*Mâ‚‚ = confâ€”â†›join Mâ†’*M'
      (â€”â†’*-trans (appL-cong ÏƒMâ†’Mâ‚) (â€”â†’*-trans (appR-cong ÏƒNâ†’Nâ‚) (_ â€”â†’âŸ¨ Î²-abs (ğ’±-V VN) âŸ© (Mâ‚' [ Nâ‚ ]) âˆ))) â€”â†›M'
âŠ¢-âŠ¨ âŠ¢true  Ïƒ GG M' (Mâ†’*M' , â€”â†›M') with refl â† â€”â†›-Mâ†’*M Mâ†’*M' (Î» { _ () }) = tt
âŠ¢-âŠ¨ âŠ¢false Ïƒ GG M' (Mâ†’*M' , â€”â†›M') with refl â† â€”â†›-Mâ†’*M Mâ†’*M' (Î» { _ () }) = tt
âŠ¢-âŠ¨ {M = if L M N} (âŠ¢if âŠ¢L âŠ¢M âŠ¢N) Ïƒ GG M' (Mâ†’*M' , â€”â†›M')
  with L' , ÏƒLâ†’L' , â€“â†›L' â† ifâ€”â†› Mâ†’*M' â€”â†›M'
  with ğ’±âŸ¦boolâŸ§-Canonical (âŠ¢-âŠ¨ âŠ¢L Ïƒ GG L' (ÏƒLâ†’L' , â€“â†›L'))
... | injâ‚ refl = âŠ¢-âŠ¨ âŠ¢M Ïƒ GG M'
      (confâ€”â†›join Mâ†’*M' (â€”â†’*-trans (if-cong ÏƒLâ†’L') (_ â€”â†’âŸ¨ Î²-ifâ‚ âŸ© subst Ïƒ M âˆ)) â€”â†›M' , â€”â†›M')
... | injâ‚‚ refl = âŠ¢-âŠ¨ âŠ¢N Ïƒ GG M'
      (confâ€”â†›join Mâ†’*M' (â€”â†’*-trans (if-cong ÏƒLâ†’L') (_ â€”â†’âŸ¨ Î²-ifâ‚‚ âŸ© subst Ïƒ N âˆ)) â€”â†›M' , â€”â†›M')

-- adequacy
-- semantically well typed term is safe
âŠ¨safe : âˆ€ {M A} â†’ âˆ… âŠ¨ M â¦‚ A â†’ Safe M
âŠ¨safe {M = M} âŠ¨M M' Mâ†’*M' with reducible? M'
... | yes M'â†’M'' = injâ‚‚ M'â†’M''
... | no  â€”â†›M'   = injâ‚ (ğ’±-V (âŠ¨M ids (Î» ()) M' (âŸªidâŸ«Mâ†’*M , Î» M'' z â†’ â€”â†›M' (M'' , z))))
  where
    âŸªidâŸ«Mâ†’*M : subst ids M â€”â†’* M'
    âŸªidâŸ«Mâ†’*M = Eq.subst (_â€”â†’* M') (Eq.sym sub-id) Mâ†’*M'

safety : âˆ€ {M A} â†’ âˆ… âŠ¢ M â¦‚ A â†’ Safe M
safety âŠ¢M = âŠ¨safe (âŠ¢-âŠ¨ âŠ¢M)
